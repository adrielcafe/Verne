apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

def props = new Properties()
project.file("app.properties").withInputStream { props.load(it) }

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cafe.adriel.verne"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode AppVersion.versionCode
        versionName AppVersion.versionName

        resConfigs 'en', 'pt'
        buildConfigField 'String', 'INSTABUG_KEY', props['key.instabug']
    }
    signingConfigs {
        release {
            keyAlias props['signing.alias']
            keyPassword props['signing.password']
            storeFile file(props['signing.file'])
            storePassword props['signing.password']
        }
    }
    buildTypes {
        debug {
            useProguard false
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            buildConfigField 'Boolean', 'RELEASE', 'false'
            resValue 'string', 'app_name', 'Verne Dev'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'RELEASE', 'true'
            resValue 'string', 'app_name', 'Verne'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    androidExtensions {
        experimental true
        defaultCacheImplementation "SPARSE_ARRAY"
    }
    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}

kapt {
    useBuildCache true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':presentation')
    implementation project(':interactor')
    implementation project(':domain')
    implementation project(':data')
    implementation project(':shared')

    // Kotlin
    implementation AppLib.kotlin
    implementation AppLib.coroutines
    implementation AppLib.androidCoroutineScopesAppCompat
    implementation AppLib.androidCoroutineScopesViewModel

    // Firebase
    implementation AppLib.firebaseCore
    implementation AppLib.firebasePerf

    // Quality
    implementation AppLib.instabug
    implementation AppLib.crashlytics
    releaseImplementation AppLib.leakCanaryNoOp
    debugImplementation AppLib.leakCanaryAndroid
    debugImplementation AppLib.leakCanarySupportFragment

    // Util
    implementation AppLib.koinViewModel
    implementation AppLib.eiffel
    implementation AppLib.mmkv
    implementation AppLib.timberKt
}

apply plugin: 'com.google.gms.google-services'