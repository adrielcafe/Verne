apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

def props = new Properties()
project.file("app.properties").withInputStream { props.load(it) }

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cafe.adriel.verne"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode 1
        versionName "1.0.0"

        resConfigs 'en', 'pt'
        buildConfigField 'String', 'INSTABUG_KEY', props['key.instabug']
    }
    signingConfigs {
        release {
            keyAlias props['signing.alias']
            keyPassword props['signing.password']
            storeFile file(props['signing.file'])
            storePassword props['signing.password']
        }
    }
    buildTypes {
        debug {
            useProguard false
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            buildConfigField 'Boolean', 'RELEASE', 'false'
            resValue 'string', 'app_name', 'Verne Dev'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'RELEASE', 'true'
            resValue 'string', 'app_name', 'Verne'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    androidExtensions {
        experimental true
        defaultCacheImplementation "SPARSE_ARRAY"
    }
    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}

kapt {
    useBuildCache true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"
    implementation "com.github.adrielcafe.androidcoroutinescopes:appcompat:$androidcoroutinescopes_version"
    implementation "com.github.adrielcafe.androidcoroutinescopes:viewmodel:$androidcoroutinescopes_version"

    // Android
    implementation "com.google.android.material:material:1.1.0-alpha04"
    implementation "androidx.appcompat:appcompat:1.1.0-alpha03"
    implementation "androidx.preference:preference:1.1.0-alpha04"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0-alpha03"
    implementation "androidx.browser:browser:1.0.0"

    // Android KTX
    implementation "androidx.core:core-ktx:1.1.0-alpha05"
    implementation "androidx.preference:preference-ktx:1.1.0-alpha04"
    implementation "androidx.fragment:fragment-ktx:1.1.0-alpha05"

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.8"
    implementation "com.google.firebase:firebase-perf:16.2.4"

    // DI
    implementation "org.koin:koin-android-viewmodel:2.0.0-rc-1"

    // State
    implementation "com.github.etiennelenhart:eiffel:4.1.0"

    // Quality
    implementation "com.instabug.library:instabug:8.1.2"
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.9"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanary_version"

    // View
    implementation "com.github.adrielcafe:krumbsview:1.1.2"
    implementation "com.mikepenz:fastadapter:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-commons:$fastadapter_version"
    implementation "com.mikepenz:fastadapter-extensions:$fastadapter_version"
    implementation "com.github.joaquimley:faboptions:1.2.0"
    implementation "com.github.Ferfalk:SimpleSearchView:0.1.3"
    implementation "com.github.sephiroth74:NumberSlidingPicker:1.0.2"
    implementation "cz.kinst.jakub:android-stateful-layout-base:2.0.8"

    // Util
    implementation "com.github.ajalt:timberkt:1.5.1"
    implementation "com.github.ravindu1024:android-keyboardlistener:1.0.0"
    implementation "org.ocpsoft.prettytime:prettytime:4.0.2.Final"
    implementation "com.github.franmontiel:LocaleChanger:0.9.2"
    implementation "com.uttampanchasara.pdfgenerator:pdfgenerator:1.1"
    implementation "com.afollestad:assent:2.2.3"

    // Forks
    // https://github.com/adrielcafe/AztecEditor-Android
    implementation "com.github.adrielcafe.AztecEditor-Android:aztec:$azteceditor_version"
    // https://github.com/adrielcafe/material-dialogs
    implementation "com.github.adrielcafe.material-dialogs:core:$materialdialogs_version"
    implementation "com.github.adrielcafe.material-dialogs:input:$materialdialogs_version"
    implementation "com.github.adrielcafe.material-dialogs:files:$materialdialogs_version"

}

apply from: '../spotless.gradle'
apply plugin: 'com.google.gms.google-services'