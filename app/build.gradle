apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

apply from: '../version.gradle'
apply from: '../dependencies.gradle'

def props = new Properties()
project.file("app.properties").withInputStream { props.load(it) }

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cafe.adriel.verne"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode appVersionCode
        versionName appVersionName

        resConfigs 'en', 'pt'
        buildConfigField 'String', 'INSTABUG_KEY', props['key.instabug']
    }
    signingConfigs {
        release {
            keyAlias props['signing.alias']
            keyPassword props['signing.password']
            storeFile file(props['signing.file'])
            storePassword props['signing.password']
        }
    }
    buildTypes {
        debug {
            useProguard false
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            buildConfigField 'Boolean', 'RELEASE', 'false'
            resValue 'string', 'app_name', 'Verne Dev'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'Boolean', 'RELEASE', 'true'
            resValue 'string', 'app_name', 'Verne'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin/'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    androidExtensions {
        experimental true
        defaultCacheImplementation "SPARSE_ARRAY"
    }
    packagingOptions {
        exclude 'META-INF/library_release.kotlin_module'
    }
}

kapt {
    useBuildCache true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':presentation')
    implementation project(':interactor')
    implementation project(':domain')
    implementation project(':data')
    implementation project(':shared')

    // Kotlin
    implementation appDependencies.kotlin
    implementation appDependencies.coroutines
    implementation appDependencies.androidCoroutineScopesAppCompat
    implementation appDependencies.androidCoroutineScopesViewModel

    // Firebase
    implementation appDependencies.firebaseCore
    implementation appDependencies.firebasePerf

    // Quality
    implementation appDependencies.instabug
    implementation appDependencies.crashlytics
    releaseImplementation appDependencies.leakCanaryNoOp
    debugImplementation appDependencies.leakCanaryAndroid
    debugImplementation appDependencies.leakCanarySupportFragment

    // Util
    implementation appDependencies.koinViewModel
    implementation appDependencies.eiffel
    implementation appDependencies.mmkv
    implementation appDependencies.timberKt
}

apply plugin: 'com.google.gms.google-services'